#================================================================================
# Vimage.rogue
# February 19, 2023
#
# a            - Apply Macro
# A            - Apply Macro to All (current image and all below)
# b
# B            -
# c            - Crop Mode - I/J/K/L crops left/bottom/top/right
# C
# d
# D
# e
# E
# f            - Horizontal Flip
# F            - Vertical Flip
# g
# G
# h            - ZOOM MODE: scroll Left. CROP MODE: crop left.
# H
# i
# I
# j            - Down / Next Image. ZOOM MODE: scroll down. CROP MODE: crop bottom.
# J            - Move image down 1 level
# k            - Up / Previous Image. ZOOM MODE: scroll up. CROP MODE: crop top.
# K            - Move image up 1 level
# l            - ZOOM MODE: scroll right. CROP MODE: crop right.
# L
# m
# M
# n
# N
# p            - Paste image below
# P            - Paste image above
# q            - Quit (close) current buffer
# Q            - Quit (close) all buffers
# r            - Rotate 90ยบ CW
# R            - Rotate 90ยบ CCW
# s            - Join horizontal (2x1)
# S            - Join vertical (1x2)
# t            - S(T)ACK - cur buffer becomes layer of image underneath
# T
# u            - UNDO
# U
# v
# V
# w
# W
# x
# X
# y [rgbay]    - Yank (copy) R, G, B, A, or Y=all channels to clipboard.
# Y
# z            - ZOOM IN  (scale * 2)
# Z            - ZOOM OUT (scale / 2)
# <            - Rotate RGB bits to the left
# >            - Rotate RGB bits to the right
# "            - Toggle between internal and OS clipboards
# :bg <color>
# :join W H
# :swap [argb][argb01]
#
# <color>
#   [V|RGB|ARGB|RRGGBB|AARRGGBB|transparent|black|white|...]
#================================================================================

$define VERSION "0.1"
$define DATE    "February 19, 2023"

$requireRogue "2.12"

module Vimage

uses Console/CommandLineParser
uses Bitmap
uses Bitmap/IO
uses Control/Action
uses Epilog
uses UI
uses Utility/Clipboard

$include Actions
$include BlitMode
$include Cmd
$include CmdLineHandler
$include Image
$include ImageList
$include ImageView
$include KeyHandler
$include State
$include StatusBar
$include TitleBar
$include VimageUI

#nativeHeader @|#include "../Source/ObjC-Interface.h"

try
  #native @|ObjC_check_file( "Test.txt" );
  Vimage.init( System.command_line_arguments )
catch (error:Error)
  Console.error.println error
  System.exit 1
endTry

class Vimage [singleton]
  DEFINITIONS
    MAX_UNDOS = 100

  PROPERTIES
    clipboard          : Bitmap
    using_os_clipboard : Logical
    repeat_count       : Int32?
    last_repeat_count  : Int32
    last_cmd           : Cmd
    anchor             = Anchor.CENTER
    bg_color           = Color.BLACK : Color
    undo_stack         = @[]
    redo_stack         = @[]
    macro              = Cmd[]
    recording_macro    : Logical

  METHODS
    method init( args:String[] )
      local command = parse_args( args )

      #trace command
      # has //options and possibly //args

      if (command//options//help)
        print_usage
        System.exit 0
      endIf

      local filenames = command//args.to_list<<String>>
      filenames.sort( (a,b)=>a.compare_to(b,&ignore_case) < 0 )

      forEach (filepath in filenames)
        local file = File( filepath )
        if (file.is_folder)
          local files = Files( file.filepath )
          files.add( "*.bmp" )
          files.add( "*.jpg" )
          files.add( "*.jpeg" )
          files.add( "*.png" )
          local nested_filepaths = files->String[]
          nested_filepaths.sort( (a,b)=>a.compare_to(b,&ignore_case) < 0 )
          forEach (nested_filepath in nested_filepaths)
            ImageList.add( Image(File(nested_filepath)) )
          endForEach
        else
          ImageList.add( Image(file) )
        endIf
      endForEach

      if (ImageList.count)
        ImageList.select( ImageList.first_child->(as ImageListItem) )
      endIf

      VimageUI.run

    method execute( cmd:Cmd, &skip_undo_save_state )
      if (cmd.is_edit)
        if (not skip_undo_save_state) save_undo
        last_cmd = cmd
      endIf
      if (recording_macro and cmd.allowed_in_macro) macro.add( cmd )
      cmd()

    method parse_args( args:String[] )->Value
      local command = CommandLineParser().
      [
        #option( "--flag",     &alias="-f" )
        option( "--help",     &aliases=["-h","-?"] )
        #option( "--setting=", &alias="-s" )
      ].parse( args )
      return command

    method print_usage
      println "Vimage v$"(VERSION)
      println "$"(DATE)
      println

      println @|USAGE
               |  vimage [OPTIONS] [filepaths]
               |
               |OPTIONS
               |  --help, -h, -?
               |    Show this help text.

    method read_and_reset_repeat_count->Int32
      local result = which{ repeat_count || 1 }.clamped_low(1)
      repeat_count = null
      return result

    method repeat_count_string->String
      if (repeat_count) return repeat_count.value + ""
      else              return ""

    method save_undo
      if (undo_stack.count > MAX_UNDOS) undo_stack.remove_first
      local state  = ImageList->Value
      undo_stack.add( state )
      redo_stack.clear

    method undo
      if (undo_stack.is_empty) return
      redo_stack.add( ImageList->Value )  # save current state as a redo
      ImageList.unpack( undo_stack.remove_last )

    method redo
      if (redo_stack.is_empty) return
      undo_stack.add( ImageList->Value )  # save current state as an undo
      ImageList.unpack( redo_stack.remove_last )

endClass
