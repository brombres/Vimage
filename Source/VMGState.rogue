class VMGState
  DEFINITIONS
    SHIFT   = (1:<<:16)
    CONTROL = (2:<<:16)
    ALT     = (4:<<:16)
    OS      = (8:<<:16)

  GLOBAL PROPERTIES
    current = VMGState()

  GLOBAL METHODS
    method change_state( new_state:VMGState )
      current.set_state( new_state )

  PROPERTIES
    key_handlers = [Int32:VMGKeyHandler]

  METHODS
    method cancel
      state = ImageListState
      set_status( "[cancelled]", &dim )

    method execute( cmd:VMGCmd )
      Vimage.execute( cmd )

    method on( e:KeyEvent )
      if (e.is_press)
        if local handler = key_handlers[ (e.modifier_key_state:<<:16) | e.keycode ]
          handler.on( e )
          return
        endIf
      endIf

    method on_enter
      noAction

    method on_leave
      noAction

    method set_state( new_state:VMGState )
      current.on_leave
      current = new_state
      current.on_enter

    method set_status( text:String, &dim )
      use builder = VMGStatusBar
        if (dim) builder.style.dim
        builder.print text
        if (dim) builder.style.dim_off
      endUse

    method state_name->String
      return ""
endClass

class ImageListState : VMGState [singleton]
  METHODS
    method init
      key_handlers[Keycode.SEMICOLON+SHIFT] = VMGKeyHandlerColon
      key_handlers[Keycode.D]               = VMGKeyHandler( DState )
      key_handlers[Keycode.F]               = VMGKeyHandlerHFlip
      key_handlers[Keycode.F+SHIFT]         = VMGKeyHandlerVFlip
      key_handlers[Keycode.J]               = VMGKeyHandlerJBuffer
      key_handlers[Keycode.K]               = VMGKeyHandlerKBuffer
      key_handlers[Keycode.N]               = VMGKeyHandlerJoinH
      key_handlers[Keycode.N+SHIFT]         = VMGKeyHandlerJoinV
      key_handlers[Keycode.PERIOD]          = VMGKeyHandlerPeriod
      key_handlers[Keycode.Q]               = VMGKeyHandlerQBuffer
      key_handlers[Keycode.J+SHIFT]         = VMGKeyHandlerShiftJBuffer
      key_handlers[Keycode.K+SHIFT]         = VMGKeyHandlerShiftKBuffer
      key_handlers[Keycode.R]               = VMGKeyHandlerRotate90CW
      key_handlers[Keycode.R+SHIFT]         = VMGKeyHandlerRotate90CCW
      key_handlers[Keycode.Z]               = VMGKeyHandlerZ
      key_handlers[Keycode.Z+SHIFT]         = VMGKeyHandlerShiftZ

    method on( e:KeyEvent )
      if (e.is_press)
        if (e.keycode >= Keycode.NUMBER_0 and e.keycode <= Keycode.NUMBER_9)
          local digit = (e.keycode - Keycode.NUMBER_0)
          if (Vimage.repeat_count)
            Vimage.repeat_count = Vimage.repeat_count.value * 10 + digit
          else
            Vimage.repeat_count = digit
            VMGStatusBar.builder.clear
          endIf
          VMGStatusBar.builder.print digit
          VMGStatusBar.request_redraw
          return
        endIf
      endIf
      prior.on( e )

    method on_enter
      Vimage.repeat_count = null

    method state_name->String
      return "IMAGE LIST"
endClass

class ZoomState : VMGState [singleton]
  METHODS
    method init
      key_handlers[Keycode.SEMICOLON+SHIFT] = VMGKeyHandlerColon
      key_handlers[Keycode.D]               = VMGKeyHandler( DState )
      key_handlers[Keycode.F]               = VMGKeyHandlerHFlip
      key_handlers[Keycode.F+SHIFT]         = VMGKeyHandlerVFlip
      key_handlers[Keycode.H]               = VMGKeyHandler( (e) => VMGImageView.scroll(RealXY(-0.5,0)) )
      key_handlers[Keycode.J]               = VMGKeyHandler( (e) => VMGImageView.scroll(RealXY(0,0.5)) )
      key_handlers[Keycode.K]               = VMGKeyHandler( (e) => VMGImageView.scroll(RealXY(0,-0.5)) )
      key_handlers[Keycode.L]               = VMGKeyHandler( (e) => VMGImageView.scroll(RealXY(0.5,0)) )
      key_handlers[Keycode.N]               = VMGKeyHandlerJoinH
      key_handlers[Keycode.N+SHIFT]         = VMGKeyHandlerJoinV
      key_handlers[Keycode.PERIOD]          = VMGKeyHandlerPeriod
      key_handlers[Keycode.Q]               = VMGKeyHandlerQBuffer
      key_handlers[Keycode.R]               = VMGKeyHandlerRotate90CW
      key_handlers[Keycode.R+SHIFT]         = VMGKeyHandlerRotate90CCW
      key_handlers[Keycode.Z]               = VMGKeyHandlerZ
      key_handlers[Keycode.Z+SHIFT]         = VMGKeyHandlerShiftZ
      key_handlers[Keycode.ESCAPE]          = VMGKeyHandler( ImageListState )

    method on( e:KeyEvent )
      if (e.is_press)
        if (e.keycode >= Keycode.NUMBER_0 and e.keycode <= Keycode.NUMBER_9)
          local digit = (e.keycode - Keycode.NUMBER_0)
          if (Vimage.repeat_count)
            Vimage.repeat_count = Vimage.repeat_count.value * 10 + digit
          else
            Vimage.repeat_count = digit
            VMGStatusBar.builder.clear
          endIf
          VMGStatusBar.builder.print digit
          VMGStatusBar.request_redraw
          return
        endIf
      endIf
      prior.on( e )

    method on_enter
      Vimage.repeat_count = null
      VMGImageList.is_dim = true
      VMGImageList.request_redraw
      VMGStatusBar.request_redraw

    method on_leave
      VMGImageList.is_dim = false
      VMGImageList.request_redraw
      VMGStatusBar.request_redraw

    method state_name->String
      return "ZOOM"
endClass

class EnterCmdState : VMGState
  METHODS
    method find_commands( keyword:String )->VMGCmdLineHandler[]
      local cmds = VMGCmdLineHandler[]
      forEach (cmd in VMGUI.cmd_line_handlers)
        if (cmd.keyword == keyword) cmds.add( cmd )
      endForEach
      if (cmds.is_empty)
        forEach (cmd in VMGUI.cmd_line_handlers)
          if (cmd.keyword.begins_with(keyword)) cmds.add( cmd )
        endForEach
      endIf
      return cmds

    method on( e:KeyEvent )
      if (e.is_press)
        if (e.is_press(Keycode.ESCAPE))
          cancel
        elseIf (e.unicode >= 33 and e.unicode != 127)
          VMGUI.command.print e.unicode
          update_status( VMGUI.command )
        elseIf (e.keycode == Keycode.BACKSPACE or e.keycode == Keycode.DELETE)
          if (VMGUI.command.count)
            VMGUI.command.remove_last
            update_status( VMGUI.command )
          endIf
        elseIf (e.keycode == Keycode.SPACE or e.keycode == Keycode.TAB or e.keycode == Keycode.ENTER)
          if (VMGUI.command.count == 0)
            if (e.keycode == Keycode.ENTER) cancel
            return
          endIf

          local cmds = find_commands( VMGUI.command.before_first(' ') )
          which (cmds.count)
            case 0
              if (e.keycode == Keycode.SPACE)
                VMGUI.command.print ' '
              elseIf (e.keycode != Keycode.TAB)
                state = ImageListState
                set_status( "[invalid command]" )
              endIf

            case 1
              local keyword = VMGUI.command.before_first(' ')
              local args = VMGUI.command.after_first( ' ' ).trimmed

              if (keyword == cmds.first.keyword)
                VMGUI.command.print ' '
              else
                VMGUI.command.clear
                VMGUI.command.print cmds.first.keyword
                VMGUI.command.print ' '
                VMGUI.command.print args
                update_status( VMGUI.command )
              endIf

              if (e.keycode == Keycode.ENTER)
                state = ImageListState
                cmds.first.execute( args )
              endIf

            others
              noAction
          endWhich
        endIf
      endIf

    method update_status( cmd_line:String )
      use builder = VMGStatusBar
        builder.print ':'

        local keyword = cmd_line.before_first(' ')
        local args = cmd_line.after_first(' ')
        local cmds = find_commands( keyword )

        which (cmds.count)
          case 0
            if (cmd_line.trimmed.count > 0)
              builder.print keyword
              if (args.count)
                builder.print ' '
                builder.print args
              endIf
              builder.print ' '
              builder.style.dim
              builder.print " [invalid command]"
              builder.style.dim_off
            endIf

          case 1
            local cmd = cmds.first
            builder.print keyword
            builder.style.dim
            builder.print cmd.keyword[ forEach in keyword.count..<cmd.keyword.count ]
            builder.style.dim_off

            if (args.count)
              builder.print ' '
              builder.print args
            endIf

            if (cmd.syntax and cmd.syntax.count)
              builder.print ' '
              builder.style.dim
              builder.print cmd.syntax
              builder.style.dim_off
            endIf

          others
            builder.print keyword
            builder.print ' '
            builder.style.dim
            builder.print '['
            forEach (cmd at i in cmds)
              if (i > 0) builder.print '|'
              builder.print cmd.keyword
            endForEach
            builder.print ']'
            builder.style.dim_off
        endWhich

      endUse
endClass

class DState : VMGState [singleton]
  METHODS
    method on_enter
      use status = VMGStatusBar
        status.print 'd'
        status.style.dim
        status.print "[d]"
        status.style.dim_off
      endUse

    method on( e:KeyEvent )
      if (e.is_press)
        if (e.keycode == Keycode.ESCAPE)
          cancel
        elseIf (e.unicode == 'd')
          execute( VMGCmdDD )
          state = ImageListState
        else
          set_status( "[invalid command 'd$']"(e.unicode) )
          state = ImageListState
        endIf
      endIf
endClass
