class VMGState
  GLOBAL PROPERTIES
    current = VMGState()

  METHODS
    method change_state( new_state:VMGState )
      on_leave
      current = new_state
      current.on_enter

    method on( e:KeyEvent )
      noAction

    method on_enter
      noAction

    method on_leave
      noAction
endClass

class IdleState : VMGState
  METHODS
    method on( e:KeyEvent )
      if (e.is_press)
        if (e.is_press(Keycode.J))
          update_status( "j (DOWN)" )
          if local current = VMGImageList.selected
            if (current.next)
              VMGImageList.select( current.next->(as VMGImageListItem) )
            endIf
          endIf
        elseIf (e.is_press(Keycode.K))
          update_status( "k (UP)" )
          if local current = VMGImageList.selected
            forEach (child in VMGImageList.children)
              if (child.next is current)
                VMGImageList.select( child->(as VMGImageListItem) )
                escapeForEach
              endIf
            endForEach
          endIf
        elseIf (e.is_press(Keycode.Q))
          VMGUI.is_running = false
        endIf
      endIf

    method update_status( text:String )
      VMGStatusBar.text = text
endClass
