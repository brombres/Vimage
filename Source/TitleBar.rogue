library Vimage
uses GUI

class TitleBar : Component [singleton]
  PROPERTIES
    image : Image
    zoom  : Real

  METHODS
    method init
      prior.init
      expand_to_fill_width
      fixed_height = 1

    method on_draw
      localize display_bounds
      ConsoleUICanvas.fill( display_bounds->Box, StyledCharacter(' ',CharacterStyle.INVERSE) )
      if (image)
        ConsoleUICanvas.cursor = display_bounds.position
        ConsoleUICanvas.style.inverse
        ConsoleUICanvas.print image
        if (zoom)
          local ratio : String
          if (zoom == 1.0 or zoom == 0)
            ratio = "1:1"
          elseIf (zoom < 1.0)
            ratio = "1:$"((1/zoom).format(".1"))
            if (ratio.ends_with(".0")) ratio .= leftmost(-2)
          else
            ratio = "$:1"(zoom->Int)
          endIf
          ConsoleUICanvas.cursor = display_bounds.top_right - XY(ratio.count,0)
          ConsoleUICanvas.print ratio
        endIf
        ConsoleUICanvas.style.inverse_off
      endIf

    method set_zoom( new_zoom:Real )
      if (new_zoom != zoom)
        @zoom = new_zoom
        request_redraw
      endIf

    method show( image )
      request_redraw

endClass
