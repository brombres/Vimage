class VMGKeyHandler
  METHODS
    method on( e:KeyEvent )
      noAction

    method set_state( new_state:VMGState )
      VMGState.current.change_state( new_state )

    method set_status( text:String )
      VMGStatusBar.text = text
endClass

class KeyHandlerColon : VMGKeyHandler [singleton]
  METHODS
    method on( e:KeyEvent )
      status = ":"
      VMGUI.command.clear
      state = EnterCmdState()
endClass

class KeyHandlerQBuffer : VMGKeyHandler [singleton]
  METHODS
    method on( e:KeyEvent )
      status = "q (Quit)"
      VMGUI.is_running = false
endClass

class KeyHandlerJBuffer : VMGKeyHandler [singleton]
  METHODS
    method on( e:KeyEvent )
      if local current = VMGImageList.selected
        status = "j (Down)"
        if (current.next)
          VMGImageList.select( current.next->(as VMGImageListItem) )
        endIf
      endIf
endClass

class KeyHandlerShiftJBuffer : VMGKeyHandler [singleton]
  METHODS
    method on( e:KeyEvent )
      if local current = VMGImageList.selected
        status = "J (Shift Down)"
        if local index = VMGImageList.locate( current )
          VMGImageList.remove( current )
          VMGImageList.insert( current, index+1 )
          VMGImageList.request_layout
        endIf
      endIf
endClass

class KeyHandlerKBuffer : VMGKeyHandler [singleton]
  METHODS
    method on( e:KeyEvent )
      status = "k (Up)"
      if local current = VMGImageList.selected
        forEach (child in VMGImageList.children)
          if (child.next is current)
            VMGImageList.select( child->(as VMGImageListItem) )
            escapeForEach
          endIf
        endForEach
      endIf
endClass

class KeyHandlerShiftKBuffer : VMGKeyHandler [singleton]
  METHODS
    method on( e:KeyEvent )
      status = "K (Shift Up)"
      if local current = VMGImageList.selected
        if local index = VMGImageList.locate( current )
          if (index > 0)
            VMGImageList.remove( current )
            VMGImageList.insert( current, index-1 )
            VMGImageList.request_layout
          endIf
        endIf
      endIf
endClass

