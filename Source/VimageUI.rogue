library Vimage
uses Console/ConsoleUI
uses UI

class VimageUI : ConsoleUI [singleton]
  PROPERTIES
    image_list_scroller : VScrollView
    repeat_count        : Int?
    command             = String()
    cmd_line_handlers   = [String:CmdLineHandler]
    last_input_timer    = Timer()
    actions             : Action

  METHODS
    method init
      prior.init
      keep_active( 2.0 )

      register( CmdLineAnchor )
      register( CmdLineAspect )
      register( CmdLineBG )
      register( CmdLineChannel )
      register( CmdLineCrop )
      register( CmdLineAspectFill )
      register( CmdLineAspectFit )
      register( CmdLineExtract )
      register( CmdLineFill )
      register( CmdLineFillAlpha )
      register( CmdLineFillRGB )
      register( CmdLineFilter )
      register( CmdLineGenerate )
      register( CmdLineHardAlpha )
      register( CmdLineInfo )
      register( CmdLineJoin )
      register( CmdLineLiteralBitwiseAND )
      register( CmdLineLiteralBitwiseOR  )
      register( CmdLineLiteralBitwiseROL )
      register( CmdLineLiteralBitwiseROR )
      register( CmdLineLiteralBitwiseXOR )
      register( CmdLineLiteralAdd )
      register( CmdLineLiteralMultiply )
      register( CmdLineName )
      register( CmdLineNEW )
      register( CmdLineNew )
      register( CmdLineOpen )
      register( CmdLineQuit )
      register( CmdLineQuitAll )
      register( CmdLineForceQuit )
      register( CmdLineForceQuitAll )
      register( CmdLineRemove )
      register( CmdLineResize )
      register( CmdLineSave )
      register( CmdLineShift )
      register( CmdLineSplit )
      register( CmdLineTrim )
      register( CmdLineWrite )
      register( CmdLineWriteAll )
      register( CmdLineWriteQuit )

      State.current.change_state( ImageListState )

      image_list_scroller = ImageListScrollView( ImageList ).[ shrink_to_fit_width ]
      local layout = (image_list_scroller + ImageView)
      layout /= TitleBar
      layout /= StatusBar
      add( layout )

    method on( e:KeyEvent )
      last_input_timer.restart
      keep_active( 1.1 )

      if (e.is_press(Keycode.ESCAPE) and actions)
        actions = null
        StatusBar.text = "[command cancelled]"
        return
      endIf

      State.current.on( e )

    method on( e:PointerEvent )
      last_input_timer.restart
      keep_active( 1.1 )
      prior.on( e )

    method on( e:ScrollEvent )
      last_input_timer.restart
      keep_active( 1.1 )
      prior.on( e )

    method on_resize
      ImageView.show( ImageView.image )

    method register( cmd_handler:CmdLineHandler )
      cmd_line_handlers[cmd_handler.keyword] = cmd_handler

    method show( image:Image )
      TitleBar.show( image )
      ImageView.show( image )

    method on_update [override]
      prior.on_update

      if (actions)
        actions .= updated
        keep_active( 0.1 )
      endIf

      # Load another image, if necessary, during user input downtime
      if (last_input_timer.elapsed_ms >= 1000)
        forEach (item in ImageList)
          local image = item->(as ImageListItem).image
          if (not image.@bitmap)
            image.bitmap
            keep_active( 0.1 )
            escapeForEach
          endIf
        endForEach
      endIf

endClass
