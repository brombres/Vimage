module Vimage
uses UI

class StatusBar : UIComponent [singleton]
  PROPERTIES
    builder = TextCanvas(Console.width,1)

  METHODS
    method init
      prior.init
      expand_to_fill_width
      fixed_height = 1

    method on_draw
      localize display_bounds
      if (Vimage.recording_macro)
        local tag = "[recording]"
        builder.cursor = XY( Console.width-(State.current.state_name.count+tag.count+1), 0 )
        builder.style.reset
        builder.style.bg_red
        builder.style.fg_white
        builder.print tag
      endIf
      builder.cursor = XY( Console.width-State.current.state_name.count, 0 )
      builder.style.reset
      builder.print State.current.state_name
      builder.flush
      builder.resize( display_bounds.size )
      ConsoleUICanvas.draw( builder, display_bounds.position )

    method on_use->TextCanvas
      builder.clear
      if (Vimage.repeat_count and Vimage.repeat_count.value > 1) builder.print Vimage.repeat_count
      return builder

    method on_end_use( builder:TextCanvas )
      builder.flush
      request_redraw

    method set_text( value:String )
      builder.clear(' ')
      builder.print( value )
      builder.flush
      request_redraw
endClass
